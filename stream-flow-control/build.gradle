plugins {
    id 'application'
	id 'com.google.protobuf' version '0.8.8'
}


repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenCentral()
    mavenLocal()
}

startScripts.enabled = false


def protobufVersion = '3.11.0'
def grpcVersion = '1.26.0'
def protocVersion = protobufVersion

dependencies {	
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	
	runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
}

protobuf {
	protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
	}
	generateProtoTasks {
		all()*.plugins { grpc {} }
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}


task helloWorldServer(type: CreateStartScripts) {
	mainClassName = 'io.jackson.oliveira.grpc.playground.GrpcServer'
	applicationName = 'hello-world-server'
	outputDir = new File(project.buildDir, 'tmp')
	classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
	from(helloWorldServer)
	fileMode = 0755
}